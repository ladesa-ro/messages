set dotenv-load := true

project_root := justfile_directory()
schema_dir := project_root / "json-schema"
schema_dist := schema_dir / "lib"
ts_dir := project_root / "integrations" / "typescript"
ts_pkg_dir := ts_dir
cs_dir := project_root / "integrations" / "dotnet"
tools_dir := project_root / ".." / ".." / "tools"
ts_generator_dir := tools_dir / "schema-to-typescript"
cs_generator_dir := tools_dir / "schema-to-csharp"
cs_project_dir := cs_dir / "Ladesa.Messages.TimetableGenerator.V1"

# Lista comandos disponíveis
default:
    @just --list

# Build tudo (schema + integrações)
build: build-schema build-integrations

# Gera JSON Schema via TypeSpec
build-schema:
    podman build -t ladesa.local/typespec-builder "{{ schema_dir }}"
    mkdir -p "{{ schema_dist }}"
    podman run --rm \
        -v "{{ schema_dist }}":/build/lib/json-schema \
        ladesa.local/typespec-builder

# Build apenas integrações (ts e cs em paralelo)
build-integrations: build-integration-ts build-integration-cs

# Gera TypeScript via quicktype
build-integration-ts:
    podman build -t ladesa.local/quicktype-ts "{{ ts_generator_dir }}"
    mkdir -p "{{ ts_pkg_dir }}/lib"
    podman run --rm \
        -v "{{ schema_dist }}":/input:ro \
        -v "{{ ts_pkg_dir }}/lib":/output \
        ladesa.local/quicktype-ts

# Gera C# via quicktype
build-integration-cs:
    podman build -t ladesa.local/quicktype-cs "{{ cs_generator_dir }}"
    mkdir -p "{{ cs_project_dir }}/Generated"
    podman run --rm \
        -v "{{ schema_dist }}":/input:ro \
        -v "{{ cs_project_dir }}/Generated":/output \
        ladesa.local/quicktype-cs

# Limpa artefatos gerados
clean:
    rm -rf "{{ schema_dist }}"
    rm -rf "{{ ts_pkg_dir }}/src"
    rm -rf "{{ cs_project_dir }}/Generated"

# Limpa imagens de container
clean-images:
    -podman rmi ladesa.local/typespec-builder ladesa.local/quicktype-ts ladesa.local/quicktype-cs
